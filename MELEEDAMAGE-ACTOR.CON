//********************************************************
//*State for Calculating Kick/Melee Damage to non-players*
//********************************************************
state add_playermeleedamage
  setvar ACTORLASTDAMAGE 0
  getactor[THISACTOR].htextra tempb
  setvarvar TEMP ACTORHP
  getactor[THISACTOR].htowner OWNERTEMP 
  getactor[OWNERTEMP].picnum PICNUMTEMP
  ifvare PICNUMTEMP APLAYER getactorvar[PLAYERID].VEHICLEID tempvehicleid

  ifvarg ACTORDEATHSWITCHDELAY 0
  {
   setactor[THISACTOR].htextra -1
   break
  }

  ifvarg oldskoolmode 0
  {
   getactor[THISACTOR].picnum temp
   ifvare temp BOSS1
   {
    getactor[THISACTOR].pal tempc
    ifvare tempc 9 mulvar tempb 4
   }
   ifvare temp BOSS3
   {
    getactor[THISACTOR].pal tempc
    ifvare tempc 9 mulvar tempb 4
   }
   ifvare temp BOSS2
   {
    getactor[THISACTOR].pal tempc
    ifvare tempc 9 mulvar tempb 4
   }
   ifvare temp BOSS4
   {
    getactor[THISACTOR].pal tempc
    ifvare tempc 9 mulvar tempb 4
   }
   setvarvar ACTORLASTDAMAGE tempb
   subvarvar ACTORHP tempb
   break
  }

  state prevent_redundantdamage

  ifvarg tempb 0 // If any actual damage is detected...
  { // ifvarg tempb 0

    ifvare mainmenu 1 // if in the menus, receive no damage and cancel event
    {
     setactor[THISACTOR].htextra -1 // 0
     break
    }

    setvar ACTORARMORDAMAGE 0
    getactor[THISACTOR].htextra KNEEDAMAGE // TODO: Leave this
    setactor[THISACTOR].htextra 1
 
    // Stage 1 of Damage Formula
    ifvarg KNEEDAMAGE 0 
    { // ifvarg KNEEDAMAGE 0 
 
     getactor[THISACTOR].htowner OWNERTEMP 
     getactor[OWNERTEMP].picnum PICNUMTEMP
     state monster_defense_calc

     ifvare PICNUMTEMP APLAYER // If the attacking actor is the Player themself...
     { // ifvare PICNUMTEMP APLAYER
      setvarvar DEFENDINGACTORDEBUG DEFENDINGACTOR

      setvarvar PLAYERATTACKTEMP PLAYERATTACK
      state watchovermeskill-attack

      // Set to [Attack * 2]
      setvarvar KNEEDAMAGE PLAYERATTACKTEMP
      mulvar KNEEDAMAGE 2
      ifp ponsteroids mulvar KNEEDAMAGE 10 // Steroids make damage 10x stronger

      // Add or Remove [Attack / 2]
      randvarvar TEMPVAR6 PLAYERATTACKTEMP
      divvar TEMPVAR6 2 
      ifp ponsteroids mulvar TEMPVAR6 10 // Steroids make damage 5x stronger

      ifrnd 128 addvarvar KNEEDAMAGE TEMPVAR6 else subvarvar KNEEDAMAGE TEMPVAR6 // Gives a Damage Error of 50% of the Physical Attack stat [+- 50% attack]

      // Add Base Damage Depending on Character and their Melee Attack Type
      ifvare CHARACTERSELECTED? 1 addvar KNEEDAMAGE 10
      ifvare CHARACTERSELECTED? 2 { ifrnd 128 addvar KNEEDAMAGE 4 else addvar KNEEDAMAGE 6 }
      ifvare CHARACTERSELECTED? 3 { ifrnd 128 addvar KNEEDAMAGE 12 else addvar KNEEDAMAGE 13 }
      ifvare CHARACTERSELECTED? 4 addvar KNEEDAMAGE 4
      ifvare CHARACTERSELECTED? 5 { addvar KNEEDAMAGE 10 randvar TEMPVAR 30 addvarvar KNEEDAMAGE TEMPVAR }
      // ifvare CHARACTERSELECTED? 6 nullop
      // ifvare CHARACTERSELECTED? 7 nullop
      // ifvare CHARACTERSELECTED? 8 nullop
      // ifvare CHARACTERSELECTED? 9 nullop
      ifvare CHARACTERSELECTED? 10 
      { 
       setvar MISCARRAYID 110 getactorvar[MISCARRAYID].MISCARRAYID_AMOUNT MISCARRAYID_AMOUNTTEMP // NETHRA-FORM?
       ifvare MISCARRAYID_AMOUNTTEMP 0 { addvar KNEEDAMAGE 22 randvar TEMPVAR 5 addvarvar KNEEDAMAGE TEMPVAR sound NEWBEAST_ATTACK } else { addvar KNEEDAMAGE 36 randvar TEMPVAR 8 addvarvar KNEEDAMAGE TEMPVAR } 
      } 
      // ifvare CHARACTERSELECTED? 11 { addvar KNEEDAMAGE 7 randvar TEMPVAR 3 addvarvar KNEEDAMAGE TEMPVAR }
      // ifvare CHARACTERSELECTED? 12 nullop
      // ifvare CHARACTERSELECTED? 13 nullop
      // ifvare CHARACTERSELECTED? 14 nullop
      ifvare CHARACTERSELECTED? 15 { ifrnd 128 { addvar KNEEDAMAGE 5 randvar TEMPVAR 1 addvarvar KNEEDAMAGE TEMPVAR } else { addvar KNEEDAMAGE 9 randvar TEMPVAR 1 addvarvar KNEEDAMAGE TEMPVAR } }

      ifvare PUBLICBETA? 0 
      {
       ifvare CHARACTERSELECTED? 10 
       { 
        setvar MISCARRAYID 110 getactorvar[MISCARRAYID].MISCARRAYID_AMOUNT MISCARRAYID_AMOUNTTEMP // NETHRA-FORM?
        ifvare MISCARRAYID_AMOUNTTEMP 0 { mulvar KNEEDAMAGE 3 divvar KNEEDAMAGE 2 } else { mulvar KNEEDAMAGE 3 } 
       } 
      }
      ifvare PUBLICBETA? 0 { ifvare CHARACTERSELECTED? 14 mulvar KNEEDAMAGE 4 }
      state checkdamagecaps-knee

      // Check If Monsters are Aireal
      // End Check

      //*******************************
      //*******************************
      //*******************************

      // Stage 2p of Damage Formula
      //***************************************
      //*Overall Damage Support [Bubsy / Lori]*
      //***************************************
      ifvare tempe APLAYER
      {
       setvar DAMAGEMODIFIER 1000
       state support-monsteroveralleffect-extra
       state damagemod-enemy-knee
      }
      state checkdamagecaps-knee

      //*******************************
      //*******************************
      //*******************************

      // Stage 3p of Damage Formula
      ifvare CHARACTERSELECTED? 1
      {
       // ***************************************************
       // *Setting up Duke's Nukage/Purplelava-Bonus Damage *
       // ***************************************************
       ifvare FLOORDAMAGETYPE? 1 mulvar KNEEDAMAGE 4
       ifvare FLOORDAMAGETYPE? 4 mulvar KNEEDAMAGE 10
       ifvare CEILINGDAMAGETYPE? 1 mulvar KNEEDAMAGE 4
       ifvare CEILINGDAMAGETYPE? 4 mulvar KNEEDAMAGE 10

       ifvare EMUFLOORDAMAGETYPE? 1 mulvar KNEEDAMAGE 4
       ifvare EMUFLOORDAMAGETYPE? 4 mulvar KNEEDAMAGE 10
       ifvare EMUCEILINGDAMAGETYPE? 1 mulvar KNEEDAMAGE 4
       ifvare EMUCEILINGDAMAGETYPE? 4 mulvar KNEEDAMAGE 10
      }

      ifvare CHARACTERSELECTED? 3
      {
       // *****************************************************
       // *Setting Up Bubsy's bonus damage when in "Mask Form*
       // *****************************************************
       setvar MISCARRAYID 103 getactorvar[MISCARRAYID].MISCARRAYID_AMOUNT MISCARRAYID_AMOUNTTEMP // BUBSY-FORM?
       ifvarg MISCARRAYID_AMOUNTTEMP 0 { mulvar KNEEDAMAGE 5 divvar KNEEDAMAGE 2 }
      }

      ifvare CHARACTERSELECTED? 4
      {
       // **************************************
       // *Setting up Ami's Water-Bonus Damage *
       // **************************************
       ifvare sector[PLAYERSECTOR].lotag 2 ifvare WATER? 2 { mulvar KNEEDAMAGE 6 } // 6x Damage
       ifvare sector[PLAYERSECTOR].lotag 1 ifvare WATER? 1 { mulvar KNEEDAMAGE 7 divvar KNEEDAMAGE 2 } // 3.5x Damage
       // *******************************
       // *Setting Up Ami's Form Bonuses*
       // *******************************
       setvar MISCARRAYID 104 getactorvar[MISCARRAYID].MISCARRAYID_AMOUNT MISCARRAYID_AMOUNTTEMP // AMI-FORM?
       ifvarl MISCARRAYID_AMOUNTTEMP 1 { mulvar KNEEDAMAGE 2 }
       ifvare MISCARRAYID_AMOUNTTEMP 1 { mulvar KNEEDAMAGE 3 divvar KNEEDAMAGE 4 }
       ifvarg MISCARRAYID_AMOUNTTEMP 1 { mulvar KNEEDAMAGE 3 }
       // ********************************
       // *Mercury Aqua Protection (Buff)*
       // ********************************
       setvar CHARACTERARRAY 4
       getactorvar[CHARACTERARRAY].CHARACTERSTATUS? TEMPVAR
       ifvarand TEMPVAR 256
       {
         ifp pducking divvar KNEEDAMAGE 20 else divvar KNEEDAMAGE 5
       }
       // ************************
       // *Mercury Super-Computer*
       // ************************
       setvar MISCARRAYID 78 getactorvar[MISCARRAYID].MISCARRAYID_AMOUNT MISCARRAYID_AMOUNTTEMP // HAVE-MERCURY-COMPUTER?
       ifvare MISCARRAYID_AMOUNTTEMP 1
       {
        ifvare player[THISACTOR].heat_on 1
        {
         mulvar KNEEDAMAGE 7
         divvar KNEEDAMAGE 4
        }
       }
      }

      ifvare CHARACTERSELECTED? 10
      {
       // **********************************
       // *Setting Up Nethra's Form Bonuses*
       // **********************************
       setvar MISCARRAYID 110 getactorvar[MISCARRAYID].MISCARRAYID_AMOUNT MISCARRAYID_AMOUNTTEMP // NETHRA-FORM?
       ifvare MISCARRAYID_AMOUNTTEMP 1
       {
        setvarvar TEMPVAR2 KNEEDAMAGE
        state checkdamage-crisisbooster
        setvarvar KNEEDAMAGE TEMPVAR2
       }
      }

      ifvare CHARACTERSELECTED? 13
      {
       // **********************************
       // *Setting Up Minako's Form Bonuses*
       // **********************************
       setvar MISCARRAYID 113 getactorvar[MISCARRAYID].MISCARRAYID_AMOUNT MISCARRAYID_AMOUNTTEMP // MINAKO-FORM?
       ifvarl MISCARRAYID_AMOUNTTEMP 1 { mulvar KNEEDAMAGE 2 }
       ifvare MISCARRAYID_AMOUNTTEMP 1 { mulvar KNEEDAMAGE 3 divvar KNEEDAMAGE 4 }
       ifvarg MISCARRAYID_AMOUNTTEMP 1 { mulvar KNEEDAMAGE 3 }
      }
      state checkdamagecaps-knee

      // Stage 4p of Damage Formula
      // *****************************
      // *Power Wrist[s] (Equippable)*
      // *****************************
      // Check for Power Wrist [equippable item]
      setvar DAMAGEMODIFIER 1000
      ifvare PLAYEREQUIPSLOTITEM1 18
      {
       randvar TEMPVAR2 25
       ifrnd 128 mulvar TEMPVAR2 -1
       addvar TEMPVAR2 250
       addvarvar DAMAGEMODIFIER TEMPVAR2
       // +22.5%-27.5% 
      }
      ifvare PLAYEREQUIPSLOTITEM2 18
      {
       randvar TEMPVAR2 25
       ifrnd 128 mulvar TEMPVAR2 -1
       addvar TEMPVAR2 250
       addvarvar DAMAGEMODIFIER TEMPVAR2
       // +22.5%-27.5% 
      }
      ifvare PLAYEREQUIPSLOTITEM3 18
      {
       randvar TEMPVAR2 25
       ifrnd 128 mulvar TEMPVAR2 -1
       addvar TEMPVAR2 250
       addvarvar DAMAGEMODIFIER TEMPVAR2
       // +22.5%-27.5% 
      }
      ifvare PLAYEREQUIPSLOTITEM4 18
      {
       randvar TEMPVAR2 25
       ifrnd 128 mulvar TEMPVAR2 -1
       addvar TEMPVAR2 250
       addvarvar DAMAGEMODIFIER TEMPVAR2
       // +22.5%-27.5% 
      }
      ifvare PLAYEREQUIPSLOTITEM5 18
      {
       randvar TEMPVAR2 25
       ifrnd 128 mulvar TEMPVAR2 -1
       addvar TEMPVAR2 250
       addvarvar DAMAGEMODIFIER TEMPVAR2
       // +22.5%-27.5% 
      }
      ifvare PLAYEREQUIPSLOTITEM6 18
      {
       randvar TEMPVAR2 25
       ifrnd 128 mulvar TEMPVAR2 -1
       addvar TEMPVAR2 250
       addvarvar DAMAGEMODIFIER TEMPVAR2
       // +22.5%-27.5% 
      }
      ifvare PLAYEREQUIPSLOTITEM7 18
      {
       randvar TEMPVAR2 25
       ifrnd 128 mulvar TEMPVAR2 -1
       addvar TEMPVAR2 250
       addvarvar DAMAGEMODIFIER TEMPVAR2
       // +22.5%-27.5% 
      }
      ifvare PLAYEREQUIPSLOTITEM8 18
      {
       randvar TEMPVAR2 25
       ifrnd 128 mulvar TEMPVAR2 -1
       addvar TEMPVAR2 250
       addvarvar DAMAGEMODIFIER TEMPVAR2
       // +22.5%-27.5% 
      }
      mulvarvar KNEEDAMAGE DAMAGEMODIFIER
      divvar KNEEDAMAGE 1000
      state checkdamagecaps-knee


      // Stage 5p of Damage Formula
      // **********************************
      // *Jewel of Four Souls [Equippable]*
      // **********************************
      setvar DAMAGEMODIFIER 1
      ifvare PLAYEREQUIPSLOTITEM1 28 addvar DAMAGEMODIFIER 10
      ifvare PLAYEREQUIPSLOTITEM2 28 addvar DAMAGEMODIFIER 10
      ifvare PLAYEREQUIPSLOTITEM3 28 addvar DAMAGEMODIFIER 10
      ifvare PLAYEREQUIPSLOTITEM4 28 addvar DAMAGEMODIFIER 10
      ifvare PLAYEREQUIPSLOTITEM5 28 addvar DAMAGEMODIFIER 10
      ifvare PLAYEREQUIPSLOTITEM6 28 addvar DAMAGEMODIFIER 10
      ifvare PLAYEREQUIPSLOTITEM7 28 addvar DAMAGEMODIFIER 10
      ifvare PLAYEREQUIPSLOTITEM8 28 addvar DAMAGEMODIFIER 10
      ifvarg DAMAGEMODIFIER 1 subvar DAMAGEMODIFIER 1
      mulvarvar KNEEDAMAGE DAMAGEMODIFIER
      state checkdamagecaps-knee

      // Stage 6p of Damage Formula
      //****************
      //*?????? ?? ????*
      //****************
      ifvarn ENEMYHASSHIKON? 0 divvar KNEEDAMAGE 10
      state checkdamagecaps-knee

      // Stage 7p of Damage Formula
      //***********************
      //*Attacker is Poisoned?*
      //***********************
      ifvarand PLAYERSTATUS? 8
      {
       randvar TEMPVAR 125
       ifrnd 128 mulvar TEMPVAR -1
       addvar TEMPVAR 500
       mulvarvar KNEEDAMAGE TEMPVAR
       divvar KNEEDAMAGE 1000
      }
      state checkdamagecaps-knee

      // Stage 8p of Damage Formula
      //***********************
      //*Defender is Poisoned?*
      //***********************
      ifvarand ACTORSTATUS? 8
      {
       randvar TEMPVAR 125
       ifrnd 128 mulvar TEMPVAR -1
       addvar TEMPVAR 1500
       mulvarvar KNEEDAMAGE TEMPVAR
       divvar KNEEDAMAGE 1000
      }
      state checkdamagecaps-knee

      // Stage 9p of Damage Formula
      // **********************
      // *Triad Booster (Buff)*
      // **********************
      ifvarand PLAYERSTATUS? 4096
      {
       randvar temp 25
       setvar tempb 250
       ifrnd 128 addvarvar tempb temp else subvarvar tempb temp
       setvar tempc 1000
       addvarvar tempc tempb
       // 122.5%-127.5% 

       mulvarvar KNEEDAMAGE tempc
       divvar KNEEDAMAGE 1000
      }

      state checkdamagecaps-knee

      // Stage 10p of Damage Formula
      // *****************
      // *Jump Kick Bonus*
      // *****************
      ifvarn sector[PLAYERSECTOR].lotag 2
      {
       getplayer[THISACTOR].jetpack_on TEMPVAR3
       ifvare TEMPVAR3 0
       {
        ifvare INTHEAIR? 1
        {
         randvar temp 166
         setvar tempb 500
         ifrnd 128 addvarvar tempb temp else subvarvar tempb temp
         setvar tempc 1000
         addvarvar tempc tempb
         // 133.4%-166.6% 

         mulvarvar KNEEDAMAGE tempc
         divvar KNEEDAMAGE 1000
        }
       }
      }
      state checkdamagecaps-knee

      // Stage 11p of Damage Formula
      // *********************
      // *Damage Shell (Buff)*
      // *********************
      ifvarand ACTORSTATUS? 512
      {
       sound SHELL_HIT
       randvar temp 33
       setvar tempb 333
       ifrnd 128 addvarvar tempb temp else subvarvar tempb temp
       ifvarg tempb 1000 setvar tempb 1000
       setvar tempc 1000
       subvarvar tempc tempb
       // -30.0%-36.6% 

       mulvarvar KNEEDAMAGE tempc
       divvar KNEEDAMAGE 1000
      }
      state checkdamagecaps-knee

      // Stage 12p of Damage Formula
      // ****************************************
      // *Setting Up Death-based Support Bonuses*
      // ****************************************
      setvar DAMAGEMODIFIER 1000
      state playersupport-deadcharacters
      mulvarvar KNEEDAMAGE DAMAGEMODIFIER
      divvar KNEEDAMAGE 1000
      state checkdamagecaps-knee

      // Stage 13p of Damage Formula
      // **************************************************
      // *Setting up Critical Hits [Norm/Super/Mega/Ultra]*
      // **************************************************
      ifvarg ACTORHP 0
      {
       state calccriticalhits
       mulvarvar KNEEDAMAGE CRITICALHITMULTI 
       ifvarvarg KNEEDAMAGE STATLIMITMAXIMUM setvarvar KNEEDAMAGE STATLIMITMAXIMUM
       // ifvarg CRITICAL? 0 addlogvar PLAYERDAMAGE
      }
      state checkdamagecaps-knee

      // Stage 14p of Damage Formula
      // *************************************************
      // *Setting up Special Formulas for Defense Factors*
      // *************************************************
      getactor[THISACTOR].picnum temp
      state assessactortype
      setvarvar ACTORDEFENSETEMP ACTORDEFENSE
 
      state serpenthelmet_check

      ifvarand CHEATBITFIELD? 8
      {
       ifvare HITACTORTYPE? 1
       {
        ifvare CHARACTERSELECTED? 15
        {
         ifvarand PLAYERSTATUS? 2048 divvar ACTORDEFENSETEMP 4
        }
        else
        ifvare CHARACTERSELECTED? 14
        {
         ifvare PUBLICBETA? 0 divvar ACTORDEFENSETEMP 20 else divvar ACTORDEFENSETEMP 2
        }
        else
        ifvare CHARACTERSELECTED? 10
        {
         setvar MISCARRAYID 110 getactorvar[MISCARRAYID].MISCARRAYID_AMOUNT MISCARRAYID_AMOUNTTEMP // NETHRA-FORM?
         ifvare MISCARRAYID_AMOUNTTEMP 0 { mulvar ACTORDEFENSETEMP 13 divvar ACTORDEFENSETEMP 16 }
         else { mulvar ACTORDEFENSETEMP 3 divvar ACTORDEFENSETEMP 16 }
        }
        else
        ifvare CHARACTERSELECTED? 3
        {
         mulvar ACTORDEFENSETEMP 2
         divvar ACTORDEFENSETEMP 5
        }
 
        ifvarl ACTORDEFENSETEMP 0 setvar ACTORDEFENSETEMP 0
        subvarvar KNEEDAMAGE ACTORDEFENSETEMP
       }
       else
       {
        ifvare CHARACTERSELECTED? 15
        {
         ifvarand PLAYERSTATUS? 2048 divvar ACTORDEFENSETEMP 4
        }
        ifvarl ACTORDEFENSETEMP 0 setvar ACTORDEFENSETEMP 0
        subvarvar KNEEDAMAGE ACTORDEFENSETEMP
        state cheatersound
        mulvar KNEEDAMAGE 1000
        ifvarvarg KNEEDAMAGE STATLIMITMAXIMUM setvarvar KNEEDAMAGE STATLIMITMAXIMUM
       }
      }
      else
      { 
       ifvare CHARACTERSELECTED? 15
       {
        ifvarand PLAYERSTATUS? 2048 divvar ACTORDEFENSETEMP 4
       }
       else
       ifvare CHARACTERSELECTED? 14
       {
        ifvare PUBLICBETA? 0 divvar ACTORDEFENSETEMP 20 else divvar ACTORDEFENSETEMP 2
       }
       else
       ifvare CHARACTERSELECTED? 10
       {
        setvar MISCARRAYID 110 getactorvar[MISCARRAYID].MISCARRAYID_AMOUNT MISCARRAYID_AMOUNTTEMP // NETHRA-FORM?
        ifvare MISCARRAYID_AMOUNTTEMP 0 { mulvar ACTORDEFENSETEMP 13 divvar ACTORDEFENSETEMP 16 }
        else { mulvar ACTORDEFENSETEMP 3 divvar ACTORDEFENSETEMP 16 }
       }
       else
       ifvare CHARACTERSELECTED? 3
       {
        mulvar ACTORDEFENSETEMP 2 divvar ACTORDEFENSETEMP 5
       }
 
       ifvarl ACTORDEFENSETEMP 0 setvar ACTORDEFENSETEMP 0
       subvarvar KNEEDAMAGE ACTORDEFENSETEMP
      }
      state checkdamagecaps-knee

      // Stage 15p of Damage Formula
      //******************
      //*"Elite" Monsters*
      //******************
      ifvarand ACTORFLAGS 512 // If the monster is an "Elite" they only suffer 25% to 50% damage
      {
       randvar TEMPVAR 25
       addvar TEMPVAR 25
       mulvarvar KNEEDAMAGE TEMPVAR
       divvar KNEEDAMAGE 100
      }
      state checkdamagecaps-knee

      // Stage 16p of Damage Formula
      //*************
      //*Quad Damage*
      //*************
      ifvare tempvehicleid -1
      {
       ifvarg QUADTIME 0 mulvar KNEEDAMAGE 4
      }
      else
      {
       ifvarg actorvar[tempvehicleid].ACTORQUADTIME 0 mulvar KNEEDAMAGE 4
      }
      ifvarl KNEEDAMAGE 1 setvar KNEEDAMAGE 1
      ifvarvarg KNEEDAMAGE STATLIMITMAXIMUM setvarvar KNEEDAMAGE STATLIMITMAXIMUM
      state checkdamagecaps-knee

      // Stage 17p of Damage Formula
      //********************
      //*Check Enemy Invuln*
      //********************
      ifvarg ACTORINVULNTIME 0
      {
       ifvare PICNUMTEMP APLAYER // If the attacking actor is the player
       {
        setvarvar ACTORDEFLECTEDDAMAGE KNEEDAMAGE
        setvar KNEEDAMAGE 0
        setactor[THISACTOR].htextra 0 // TODO: Leave this
        sound INVULNITEM3
       }
       setvar ACTORINVULN? 1
      }
      else
      {
       setvar ACTORINVULN? 0
      }
      state checkdamagecaps-knee

      // Stage 18p of Damage Formula
      //*******************************************
      //*Mystery Lady's Sabre Special Attack Bonus*
      //*******************************************
      ifvare PUBLICBETA? 0
      {
       ifvare CHARACTERSELECTED? 14
       {
        stopsound KICK_HIT
        sound SHORT_CIRCUIT state sparkstate
        sound SHORT_CIRCUIT state sparkstate
        sound SHORT_CIRCUIT state sparkstate
        setactor[THISACTOR].htpicnum RADIUSEXPLOSION
        ifvarg ACTORINVULNTIME 0
        {
         ifvarg CRITICAL? 0
         {
          setvar ACTORINVULNTIME 0
          redefinequote 335 ^22DESTROYED ENEMY'S INVULNERBILITY!!
         }
         else
         {
          setvar TEMP2 10
          randvar TEMP3 5
          ifrnd 128 addvarvar TEMP2 TEMP3 else subvarvar TEMP2 TEMP3
          setvarvar TEMP3 TEMP2
          mulvar TEMP2 26
          subvarvar ACTORINVULNTIME TEMP2
          ifvarl ACTORINVULNTIME 0 setvar ACTORINVULNTIME 0
          redefinequote 335 ^22REDUCED ENEMY'S INVULNERBILITY TIME BY %d SECONDS!
          qsprintf 335 335 TEMP3
         }
         setvarvar fta fta_normal setvar ftq 335
        }
        else
        {
         randvar TEMP3 99 addvar TEMP3 1
         setvar CHARACTERARRAY 14
         getactorvar[CHARACTERARRAY].CHARACTERLEVEL TEMP2
         ifvarvarg TEMP3 TEMP2
         {
          setvarvar TEMP3 ACTORHP
          randvar TEMP2 2
          addvar TEMP2 2
          ifvarn TEMP2 0 divvarvar TEMP3 TEMP2
          addvarvar KNEEDAMAGE TEMP3

          setvarvar TEMP3 ACTORAP
          randvar TEMP2 2
          addvar TEMP2 2
          ifvarn TEMP2 0 divvarvar TEMP3 TEMP2
          addvarvar KNEEDAMAGE TEMP3
         }
         else
         {
          setvarvar TEMP3 ACTORMAXHP
          randvar TEMP2 1
          addvar TEMP2 1
          ifvarn TEMP2 0 divvarvar TEMP3 TEMP2
          addvarvar KNEEDAMAGE TEMP3

          setvarvar TEMP3 ACTORMAXAP
          randvar TEMP2 1
          addvar TEMP2 1
          ifvarn TEMP2 0 divvarvar TEMP3 TEMP2
          addvarvar KNEEDAMAGE TEMP3 

          redefinequote 335 ^22CRITICAL STRIKE!!
          setvarvar fta fta_normal setvar ftq 335
         }
        }
       }
      }
      state checkdamagecaps-knee

      // Stage 19p of Damage Formula
      // *************************
      // * IMPORTANT ARMOR START *
      // *************************
      setvarvar tempd KNEEDAMAGE

      ifvarg VAMPIRETIME 0 setvarvar TEMPVAR5 ACTORARMORPERCENT
      state enemyarmordamage
      state showdamage_actor-kneeap
      setvarvar KNEEDAMAGE tempd
      // ***********************
      // * IMPORTANT ARMOR END *
      // ***********************
      state checkdamagecaps-knee

      // Stage 20p of Damage Formula
      // ************************
      // * Check Snake Armbands *
      // ************************
      ifvarg KNEEDAMAGE 0 state snakearmband_check
      state checkdamagecaps-knee

      // Stage 21p of Damage Formula
      // *********************************************
      // * Check Melee Attacks for Confusion Ailment *
      // *********************************************
      // Damage to HP must be >5-10% (20-25% for elites) to have the chance for Blunt hits to confuse them.
      setvarvar TEMP3 ACTORMAXHP
      ifvarand ACTORFLAGS 512 // Elite
      {
       ifvarand ACTORFLAGS 1024 // Elite Boss
       {
        randvar TEMP2 125 addvar TEMP2 250	// 25.0%-37.5%
       }
       else // Only Elite
       {
        randvar TEMP2 50 addvar TEMP2 200	// 20.0%-25.0%
       }
      }
      else
      {
       ifvarand ACTORFLAGS 1024 // Elite Boss
       {
        randvar TEMP2 125 addvar TEMP2 100	// 10.0%-22.5%
       }
       else
       {
        randvar TEMP2 50 addvar TEMP2 50	// 5.0%-10.0%
       }
      }
      mulvarvar TEMP3 TEMP2
      divvar TEMP3 1000

      ifvarvarg KNEEDAMAGE TEMP3
      {
       ifvare CHARACTERSELECTED? 10
       {
        setvar MISCARRAYID 110 getactorvar[MISCARRAYID].MISCARRAYID_AMOUNT MISCARRAYID_AMOUNTTEMP // NETHRA-FORM?
        ifvare MISCARRAYID_AMOUNTTEMP 0
        {
         ifrnd 51
         {
          ifvarand ACTORSTATUS? 16
          {
           randvar TEMP 390
           addvar TEMP 390
           addvarvar ACTORDIMNESSTIMER TEMP
          } 
          else 
          { 
           addvar ACTORSTATUS? 16
           randvar TEMP 390
           addvar TEMP 390
           addvarvar ACTORDIMNESSTIMER TEMP
           redefinequote 335 ^22YOU HAVE DIMMED AN ENEMY!
           setvarvar fta fta_normal setvar ftq 335
          }   
         }   
        }
        else
        {
         ifrnd 51
         {
          ifvarand ACTORSTATUS? 128
          { 
           randvar TEMP 390
           addvar TEMP 390
           addvarvar ACTORCONFUSIONTIMER TEMP
          } 
          else 
          { 
           addvar ACTORSTATUS? 128
           randvar TEMP 390
           addvar TEMP 390
           addvarvar ACTORCONFUSIONTIMER TEMP
           redefinequote 335 ^22YOU HAVE CONFUSED AN ENEMY!
           setvarvar fta fta_normal setvar ftq 335
          }   
         }   
        }
       }
       else
       {
        ifrnd 51
        {
         ifvarand ACTORSTATUS? 128
         { 
          randvar TEMP 390
          addvar TEMP 390
          addvarvar ACTORCONFUSIONTIMER TEMP
         } 
         else 
         { 
          addvar ACTORSTATUS? 128
          randvar TEMP 390
          addvar TEMP 390
          addvarvar ACTORCONFUSIONTIMER TEMP
          redefinequote 335 ^22YOU HAVE CONFUSED AN ENEMY!
          setvarvar fta fta_normal setvar ftq 335
         }   
        }   
       }
      }
      state checkdamagecaps-knee

      // Stage 22p of Damage Formula
      //*********************
      //* Misc Arrangements *
      //*********************
      setvarvar TEMPVAR KNEEDAMAGE
      setvarvar TEMPATKCHARACTERVAR CHARACTERSELECTED?
      setvar DEFENDERDISPLAYTIMER 0
      ifvare TEMPVAR 0 setvar ACTORNULLDAMAGE 1 else setvar ACTORNULLDAMAGE 0
      setvarvar ACTORARMORDAMAGETEMP ACTORARMORDAMAGE
      setvar ACTORARMORDISPLAYTIMER 0

      ifvarg CURRENTACTORARMORPERCENT 0 { ifvare ACTORARMORDAMAGETEMP 0 setvar ACTORARMORNULLDAMAGE 1 else setvar ACTORARMORNULLDAMAGE 0 }
      state checkdamagecaps-knee
      // FINAL DAMAGE COUNT HERE

      // Stage 23p of Damage Formula
      //*********************************************************************************
      //* Drain Artifact and Twilight Cloaks [for players since this is monster damage] *
      //*********************************************************************************
      ifvarg KNEEDAMAGE 0
      {
       ifvarvare OWNERTEMP PLAYERID
       {
        ifvare tempvehicleid -1
        {
         ifvarg VAMPIREMAXDMGFACTOR 1000 // ifvarg VAMPIRETIME 0
         {
          setvarvar TEMP MAXHITPOINTS

          mulvarvar TEMP VAMPIREMAXDMGFACTOR // Heals up to 125% HP on Public Beta
          divvar TEMP 1000

          ifvarvarl CURRENTHITPOINTS TEMP
          {
           setvarvar TEMP2 KNEEDAMAGE
           ifvarvarg TEMP2 ACTORHP setvarvar TEMP2 ACTORHP
           mulvarvar TEMP2 VAMPIREDMGFACTOR
           divvar TEMP2 1000
           addvarvar CURRENTHITPOINTS TEMP2
        
           setvarvar TEMP3 MAXHITPOINTS
           mulvarvar TEMP3 VAMPIREMAXDMGFACTOR // Heals up to 125% HP on Public Beta
           divvar TEMP3 1000
  
           ifvarvarg CURRENTHITPOINTS TEMP3 setvarvar CURRENTHITPOINTS TEMP3
           ifvarg TEMP2 0
           {
            setvarvar TEMPHEALTH TEMP2
            state showhealing_playerhp

            setvar PLAYERHPDAMAGED? -1
            sound DRAIN_HP
            sound DRAIN_HP
            sound DRAIN_HP
           }
          }
         }
        }
        else
        {
         getactorvar[tempvehicleid].ACTORVAMPIREMAXDMGFACTOR TEMPVAR6 // getactorvar[tempvehicleid].ACTORVAMPIRETIME TEMPVAR5
         ifvarg TEMPVAR6 1000
         {
          getactorvar[tempvehicleid].ACTORMAXHP TEMP

          getactorvar[tempvehicleid].ACTORVAMPIREMAXDMGFACTOR TEMPVAR3
          mulvarvar TEMP TEMPVAR3 // Heals up to 125% HP on Public Beta
          divvar TEMP 1000

          getactorvar[tempvehicleid].ACTORHP TEMPVAR4
          ifvarvarl TEMPVAR4 TEMP
          {
           setvarvar TEMP2 KNEEDAMAGE
           ifvarvarg TEMP2 ACTORHP setvarvar TEMP2 ACTORHP
           getactorvar[tempvehicleid].ACTORVAMPIREDMGFACTOR TEMPVAR3
           mulvarvar TEMP2 TEMPVAR3
           divvar TEMP2 1000
           addvarvar TEMPVAR4 TEMP2
      
           getactorvar[tempvehicleid].ACTORMAXHP TEMP3
           getactorvar[tempvehicleid].ACTORVAMPIREMAXDMGFACTOR TEMPVAR3
           mulvarvar TEMP3 TEMPVAR3 // Heals up to 125% HP on Public Beta
           divvar TEMP3 1000

           ifvarvarg TEMPVAR4 TEMP3 setvarvar TEMPVAR4 TEMP3
           setactorvar[tempvehicleid].ACTORHP TEMPVAR4
           ifvarg TEMP2 0
           {
            setvarvar TEMPHEALTH TEMP2
            state showhealing_playerhp

            setvar VEHICLEHPDAMAGED? -1
            sound DRAIN_HP
            sound DRAIN_HP
            sound DRAIN_HP
           }
          }
         }
        }
       }
      }

      ifvarg ACTORARMORDAMAGE 0
      {
       ifvarvare OWNERTEMP PLAYERID
       {
        ifvare tempvehicleid -1
        {
         ifvarg VAMPIREMAXDMGFACTOR 1000 // ifvarg VAMPIRETIME 0
         {
          setvarvar TEMP MAXARMORPOINTS
 
          mulvarvar TEMP VAMPIREMAXDMGFACTOR // Heals up to 125% HP on Public Beta
          divvar TEMP 1000

          ifvarvarl CURRENTARMORPOINTS TEMP
          {
           setvarvar TEMP2 ACTORARMORDAMAGE
           mulvarvar TEMP2 VAMPIREDMGFACTOR
           divvar TEMP2 1000
           addvarvar CURRENTARMORPOINTS TEMP2
       
           setvarvar TEMP3 MAXARMORPOINTS
           mulvarvar TEMP3 VAMPIREMAXDMGFACTOR // Heals up to 125% HP on Public Beta
           divvar TEMP3 1000

           ifvarvarg CURRENTARMORPOINTS TEMP3 setvarvar CURRENTARMORPOINTS TEMP3

           ifvarvarl ARMORPERCENT ACTORARMORPERCENT setvarvar ARMORPERCENT ACTORARMORPERCENT
           ifvarg TEMP2 0
           {
            setvarvar RECEIVEDARMOR TEMP2
            state showhealing_playerap

            setvar PLAYERAPDAMAGED? -1
            sound DRAIN_AP
            sound DRAIN_AP
            sound DRAIN_AP
           }
          }
         }
        }
        else
        {
         getactorvar[tempvehicleid].ACTORVAMPIREMAXDMGFACTOR TEMPVAR6 // getactorvar[tempvehicleid].ACTORVAMPIRETIME TEMPVAR5
         ifvarg TEMPVAR6 1000
         {
          getactorvar[tempvehicleid].ACTORMAXAP TEMP

          getactorvar[tempvehicleid].ACTORVAMPIREMAXDMGFACTOR TEMPVAR3
          mulvarvar TEMP TEMPVAR3 // Heals up to 125% HP on Public Beta
          divvar TEMP 1000

          getactorvar[tempvehicleid].ACTORAP TEMPVAR4
          ifvarvarl TEMPVAR4 TEMP
          {
           setvarvar TEMP2 ACTORARMORDAMAGE
           getactorvar[tempvehicleid].ACTORVAMPIREDMGFACTOR TEMPVAR3
           mulvarvar TEMP2 TEMPVAR3
           divvar TEMP2 1000
           addvarvar TEMPVAR4 TEMP2
       
           getactorvar[tempvehicleid].ACTORMAXAP TEMP3
           getactorvar[tempvehicleid].ACTORVAMPIREMAXDMGFACTOR TEMPVAR3
           mulvarvar TEMP3 TEMPVAR3 // Heals up to 125% HP on Public Beta
           divvar TEMP3 1000

           ifvarvarg TEMPVAR4 TEMP3 setvarvar TEMPVAR4 TEMP3
           setactorvar[tempvehicleid].ACTORAP TEMPVAR4

           getactorvar[tempvehicleid].ACTORARMORPERCENT TEMPVAR4
           ifvarvarl TEMPVAR4 ACTORARMORPERCENT { setvarvar TEMPVAR4 ACTORARMORPERCENT setactorvar[tempvehicleid].ACTORARMORPERCENT TEMPVAR4 }
           ifvarg TEMP2 0
           {
            setvarvar RECEIVEDARMOR TEMP2
            state showhealing_playerap

            setvar VEHICLEAPDAMAGED? -1
            sound DRAIN_AP  
            sound DRAIN_AP
            sound DRAIN_AP
           }
          }
         }
        }
       }
      }
      // End Drain Artifact [for players since this is monster damage]
      state checkdamagecaps-knee

      // Stage 24p of Damage Formula
      ifvarg KNEEDAMAGE 0 setvar ACTORHPDAMAGED? 1
      subvarvar ACTORHP KNEEDAMAGE state showdamage_actor-kneehp
      setvarvar ACTORLASTDAMAGE KNEEDAMAGE
      ifvarg ACTORHP 0
      {
       setactor[THISACTOR].extra 8193
       setactor[THISACTOR].htextra 1
      }
      else
      {
       setvar ACTORHP 0
       setactor[THISACTOR].extra 0
       state checkactorsurvivalvalue
       ifvarg KNEEDAMAGE 32767 setactor[THISACTOR].htextra 100 else setactor[THISACTOR].htextra KNEEDAMAGE
      }
      // FINAL DAMAGE COUNT HERE

      setvarvar ACTORHEALTH ACTORHP

      setvarvar DAMAGETEMP KNEEDAMAGE 
      setvarvar TEMPHITACTORVARHP ACTORMAXHP
      setvarvar TEMPHITACTORVARAP ACTORMAXAP
      setvarvar TEMPATKCHARACTERVAR CHARACTERSELECTED?
      setvar DEFENDERDISPLAYTIMER 0
      state checkdamagecaps-knee
     } // ifvare PICNUMTEMP APLAYER
     else // if the attacking actor is not either the player or the same species, damage is divided by half BEFORE defense is factored in
     { // ifvarn PICNUMTEMP APLAYER
      // Stage 2e of Damage Formula
      state calccriticalhits_actor
      mulvarvar KNEEDAMAGE CRITICALHITMULTI
      ifvarvarg KNEEDAMAGE STATLIMITMAXIMUM setvarvar KNEEDAMAGE STATLIMITMAXIMUM
      // ifvarg ACTORCRITICAL? 0 addlogvar PLAYERDAMAGE
      state checkdamagecaps-knee

      // Stage 3e of Damage Formula
      divvar KNEEDAMAGE 2
      subvarvar KNEEDAMAGE ACTORDEFENSE
      state checkdamagecaps-knee

      // Stage 4e of Damage Formula
      // FINAL DAMAGE COUNT HERE
      ifvarg KNEEDAMAGE 0 setvar ACTORHPDAMAGED? 1
      subvarvar ACTORHP KNEEDAMAGE state showdamage_actor-kneehp
      setvarvar ACTORLASTDAMAGE KNEEDAMAGE
      ifvarg ACTORHP 0
      {
       setactor[THISACTOR].htextra 1
       setactor[THISACTOR].extra 8193
      }
      else
      {
       setvar ACTORHP 0
       setactor[THISACTOR].extra 0
       state checkactorsurvivalvalue
       setactor[THISACTOR].htextra KNEEDAMAGE
      }

      state checkdamagecaps-knee
      // FINAL DAMAGE COUNT HERE
     } // ifvarn PICNUMTEMP APLAYER
   
    } // ifvarn KNEEDAMAGE 0 
  } // ifvarg tempb 0
ends 

